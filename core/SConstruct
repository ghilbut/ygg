import os

# unix, linux, mac  : 'clear'
# microsoft windows : 'CLS'
os.system('clear' if os.name is 'posix' else 'CLS')

BASEPATH = Dir('.').abspath

print '-----------------------------------------------------------------'
print '[YGG]  build ygg core library and run test,'
print '       on %s.' % BASEPATH
print '-----------------------------------------------------------------'



# build core library
print '    ======== build core library'

CPPPATH = [
    '../ext/include',
    '../',
  ]

env = Environment(CPPPATH=CPPPATH, CXXFLAGS=['-std=c++11', '-g', '-rdynamic'])



sources = [
    'connection.cpp',
    'ctrl_desc.cpp',
    'local_connection.cpp',
    'object.cpp',
    'target_desc.cpp',
    'target_proxy.cpp',
  ]

lib = env.StaticLibrary(target='core', source=sources)
env.Install('../lib', lib)
env.Alias('install', '../lib')



# build and run unittest
print '    ======== build core unittest'

# core
sources = [
    '../ext/src/gmock_main.cc',
    '../test/fake.cpp',  # this should be seperated library
    '../test/vars.cpp',  # this should be seperated library
    'connection_test.cpp',
    'ctrl_desc_test.cpp',
    'object_test.cpp',
    #'proxy_test.cpp',
    'target_desc_test.cpp',
    'target_proxy_test.cpp',
  ]

LIBS = [
    'gtest', 
    'jsoncpp',
    'core',
  ]

LIBPATH = [
    '../ext/lib',
    '../lib',
  ]

if os.name == 'posix':
  env.Append(LINKFLAGS=['-pthread'])
test = env.Program(target='test', source=sources, LIBS=LIBS, LIBPATH=LIBPATH)



# run after build complete
print '    ======== run core unittest'

Command(
  'unittest', 
  test, 
  lambda env, target, source: os.system(source[0].abspath))

